import snakebids
from snakebids import bids, generate_inputs, get_wildcard_constraints

configfile: '/config/snakebids.yml'

# Get input wildcards
inputs = generate_inputs(
    bids_dir=config["bids_dir"],
    pybids_inputs=config["pybids_inputs"],
    pybidsdb_dir=config.get("pybidsdb_dir"),
    pybidsdb_reset=config.get("pybidsdb_reset"),
    derivatives=config.get("derivatives", None),
    participant_label=config.get("participant_label", None),
    exclude_participant_label=config.get("exclude_participant_label", None),
)

# this adds constraints to the bids naming
wildcard_constraints:
    **get_wildcard_constraints(config["pybids_inputs"]),

# ----
wildcard_constraints:
    desc="[a-zA-Z0-9]+",
    space="[a-zA-Z0-9]+",
    hemi="[a-zA-Z0-9]+",
    surfname="[a-zA-Z0-9]+",
    modality="[a-zA-Z0-9]+",
    density="[a-zA-Z0-9]+",
    atlas="[a-zA-Z0-9]+",
    autotop="[a-zA-Z0-9]+",
    template="[a-zA-Z0-9]+",

root = os.path.expandvars(config["root"])
work = os.path.expandvars(config["workdir"])
resolution = float(config['res'])

# bias correction for gad files
rule n4_bias_correction:
    input:
        im = bids(
            root=str(Path(config["bids_dir"])),
            datatype="anat",
            suffix="T1w.nii.gz",
            **inputs["t1w"].wildcards
        )     
    output:
        corrected_im = bids(
            root=work,
            datatype="bias_correction",
            desc="n4_bias_corr",
            suffix="T1w.nii.gz",
            **inputs["t1w"].wildcards
        )
    script:
        "scripts/n4_bias_corr.py"

# isotropic resampling
rule isotropic_resampling:
    input:
        input_im = bids(
            root=work,
            datatype="bias_correction",
            desc="n4_bias_corr",
            suffix="T1w.nii.gz",
            **inputs["t1w"].wildcards
        ) 
    params:
        res=resolution
    output:
        resam_im = bids(
            root=work,
            datatype="resampled",
            desc="resampled",
            res=config["res"],
            suffix="T1w.nii.gz",
            **inputs["t1w"].wildcards
        ) 
    script:
        "scripts/resample_img.py"

# image registration rigid
rule run_greedy_rigid:
    input:
        nongad_moving = bids(
            root=work,
            datatype="resampled",
            desc="resampled",
            res=config["res"],
            suffix="T1w.nii.gz",
            acq="nongad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        ),
        gad_fixed = bids(
            root=work,
            datatype="resampled",
            desc="resampled",
            res=config["res"],
            suffix="T1w.nii.gz",
            acq="gad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        )
    output:
        rigid_mat = bids(
            root=work,
            datatype="greedy",
            desc="nongad_to_gad_rigid",
            suffix=".mat",
            acq="gad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        )
    container: config["singularity"]["itksnap"]
    shell:
        "greedy -d 3 -a -dof 6 -m NCC 2x2x2 -i {input.gad_fixed} {input.nongad_moving} -o {output.rigid_mat} -ia-image-centers -n 100x50x0"

# apply registration rigid
rule apply_registration_rigid:
    input:
        nongad_moving = bids(
            root=work,
            datatype="resampled",
            desc="resampled",
            res=config["res"],
            suffix="T1w.nii.gz",
            acq="nongad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        ),
        gad_fixed = bids(
            root=work,
            datatype="resampled",
            desc="resampled",
            res=config["res"],
            suffix="T1w.nii.gz",
            acq="gad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        ),
        rigid_mat = bids(
            root=work,
            datatype="greedy",
            desc="nongad_to_gad_rigid",
            suffix=".mat",
            acq="gad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        )
    output:
        registered_nongad = bids(
            root=work,
            datatype="greedy",
            desc="nongad_to_gad_rigid",
            suffix="T1w.nii.gz",
            acq="nongad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        )
    container: config["singularity"]["itksnap"]
    shell:
        "greedy -d 3 -dof 6 -rf {input.gad_fixed} -rm {input.nongad_moving} {output.registered_nongad} -r {input.rigid_mat}"

# image registration affine
rule run_greedy_affine:
    input:
        nongad_moving = bids(
            root=work,
            datatype="resampled",
            desc="resampled",
            res=config["res"],
            suffix="T1w.nii.gz",
            acq="nongad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        ),
        gad_fixed = bids(
            root=work,
            datatype="resampled",
            desc="resampled",
            res=config["res"],
            suffix="T1w.nii.gz",
            acq="gad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        )
    output:
        rigid_mat = bids(
            root=work,
            datatype="greedy",
            desc="nongad_to_gad_affine",
            suffix=".mat",
            acq="gad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        )
    container: config["singularity"]["itksnap"]
    shell:
        "greedy -d 3 -a -dof 12 -m NCC 2x2x2 -i {input.gad_fixed} {input.nongad_moving} -o {output.rigid_mat} -ia-image-centers -n 100x50x0"

# apply registration rigid
rule apply_registration_affine:
    input:
        nongad_moving = bids(
            root=work,
            datatype="resampled",
            desc="resampled",
            res=config["res"],
            suffix="T1w.nii.gz",
            acq="nongad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        ),
        gad_fixed = bids(
            root=work,
            datatype="resampled",
            desc="resampled",
            res=config["res"],
            suffix="T1w.nii.gz",
            acq="gad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        ),
        rigid_mat = bids(
            root=work,
            datatype="greedy",
            desc="nongad_to_gad_affine",
            suffix=".mat",
            acq="gad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        )
    output:
        registered_nongad = bids(
            root=work,
            datatype="greedy",
            desc="nongad_to_gad_affine",
            suffix="T1w.nii.gz",
            acq="nongad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        )
    container: config["singularity"]["itksnap"]
    shell:
        "greedy -d 3 -dof 12 -rf {input.gad_fixed} -rm {input.nongad_moving} {output.registered_nongad} -r {input.rigid_mat}"

# normalize non gad with rigid registration applied
rule nongad_zscore_normalization:
    input:
        norm_in = bids(
            root=work,
            datatype="greedy",
            desc="nongad_to_gad_rigid",
            suffix="T1w.nii.gz",
            acq="nongad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        )
    output:
        norm_out = bids(
            root=work,
            datatype="normalize",
            desc="normalized_zscore",
            suffix="T1w.nii.gz",
            acq="nongad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        )
    params: 
        norm_method="zscore"
    script: 'scripts/normalize.py'

# normalize gad
rule gad_minmax_normalization:
    input:
        norm_in = bids(
            root=work,
            datatype="resampled",
            desc="resampled",
            res=config["res"],
            suffix="T1w.nii.gz",
            acq="gad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"} 
        )
    output:
        norm_out = bids(
            root=work,
            datatype="normalize",
            desc="normalize_minmax",
            suffix="T1w.nii.gz",
            acq="gad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"} 
        )
    params: norm_method="minmax"
    script: 'scripts/normalize.py'

# create patches for model inputs 
rule create_patches:
    input:
        norm_nongad = bids(
            root=work,
            datatype="normalize",
            desc="normalized_zscore",
            suffix="T1w.nii.gz",
            acq="nongad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"}
        ),
        norm_gad = bids(
            root=work,
            datatype="normalize",
            desc="normalize_minmax",
            suffix="T1w.nii.gz",
            acq="gad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"} 
        )
    params:
        radius_vector = '31x31x31', #patch dimensions will be (x*2)+1 voxels
        n = '5', #sample n randomly augment patches
        angle = '10', #stdev of normal distribution for sampling angle (in degrees)
        frequency = '4500' #sample 1 patch for every n voxels
    output:
        patches = bids(
            root=work,
            datatype="patches",
            desc="samples_31",
            suffix=".dat",
            acq="gad",
            **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"} 
        )
    resources:    
        mem_mb = 32000    
    container:
        config["singularity"]["itksnap"]
    shell: 'c3d {input.norm_gad} {input.norm_nongad} {input.norm_nongad} -xpa {params.n} {params.angle} -xp {output.patches} {params.radius_vector} {params.frequency}'

if config["qc"]:
    print(f"..... QC Enabled .....")
    include: "rules/qc.smk"

rule all:
    input:
        patches = inputs["t1w"].expand(
            bids(
                root=work,
                datatype="patches",
                desc="samples_31",
                suffix=".dat",
                acq="gad",
                **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"} 
            )
        ),
        qc=inputs["t1w"].expand(
            bids(
                root=root,
                datatype="prepocessing_qc",
                desc="qc",
                suffix=".html",
                acq="gad",
                **{k: v for k, v in inputs["t1w"].wildcards.items() if k != "acq"} 
            )
        )
        if config["qc"]
        else [],
       
 
